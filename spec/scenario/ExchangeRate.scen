
Test "Initial Exchange Rate"
    NewComptroller
    NewSToken ZRX sZRX initialExchangeRate:2e9
    Assert Equal (SToken sZRX ExchangeRateStored) (Exp 2e9)

Test "Initial Exchange Rate with Mint"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:5e9
    -- Check initial exchange holds
    Invariant Remains (SToken sZRX ExchangeRateStored) (Exp 5e9)
    -- Mint some tokens and verify invariant still holds
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 sZRX TotalSupply) (Exactly 10e9)
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 10e9)
    -- Let's mint again and verify it's still good.
    Prep Torrey Some ZRX sZRX
    Mint Torrey 50e18 sZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 sZRX TotalSupply) (Exactly 20e9)
    Assert Equal (Erc20 sZRX TokenBalance Torrey) (Exactly 10e9)

Test "ZRX: Exch. Rate:2e9, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=18, Decimals Us=8 -> 2e9 Exchange Rate
    ListedSToken ZRX sZRX initialExchangeRate:2e9 decimals:8
    Assert Equal (SToken sZRX ExchangeRateStored) (Exp 2e9)
    -- Mint 50.0 ZRX at given exchange rate
    Prep Geoff Some ZRX sZRX
    Assert Equal (Erc20 sZRX TotalSupply) Zero
    Mint Geoff 50e18 sZRX
    -- 50e18 / 2e9 = 2.5e10
    Assert Equal (Erc20 sZRX TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Erc20 ZRX Faucet sZRX 1.0e18
    Assert Equal (Erc20 ZRX TokenBalance sZRX) (Exactly 51.0e18)
    -- Mock total borrows
    SToken sZRX Mock totalBorrows 2.0e18
    Assert Equal (SToken sZRX TotalBorrows) (Exactly 2.0e18)
    -- Mock total reserves
    SToken sZRX Mock totalReserves 0.5e18
    Assert Equal (SToken sZRX Reserves) (Exactly 0.5e18)
    -- Okay, we're all set, let's check the exchange rate
    -- (51+2-0.5)e18/2.5e10 = 52.5e18/2.5e10 = 21e8 -> 21e26 (Exp)
    Assert Equal (SToken sZRX ExchangeRateStored) (Exp 2.1e9)

Test "USDC: Exch. Rate:2e-3, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=6, Decimals Us=8 -> 2e-3 Exchange Rate
    ListedSToken USDC sUSDC initialExchangeRate:2e-3 decimals:8
    Assert Equal (SToken sUSDC ExchangeRateStored) (Exp 2e-3)
    -- Mint 50.0 USDC at given exchange rate
    Prep Geoff Little USDC sUSDC
    Assert Equal (Erc20 sUSDC TotalSupply) (Exactly 0.0)
    Mint Geoff 50e6 sUSDC
    -- 5.0e7 / 2e-3 = 2.5e10
    Assert Equal (Erc20 sUSDC TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 sUSDC TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Erc20 USDC Faucet sUSDC 2.0e6
    Assert Equal (Erc20 USDC TokenBalance sUSDC) (Exactly 52.0e6)
    -- Mock total borrows
    SToken sUSDC Mock totalBorrows 5.0e6
    Assert Equal (SToken sUSDC TotalBorrows) (Exactly 5.0e6)
    -- Mock total reserves
    SToken sUSDC Mock totalReserves 1.5e6
    Assert Equal (SToken sUSDC Reserves) (Exactly 1.5e6)
    -- Okay, we're all set, let's check the exchange rate
    -- (52+5-1.5)e6/2.5e10 = 55.5e6/2.5e10 = 2.22e-3 -> 2.22e15 (Exp)
    Assert Equal (SToken sUSDC ExchangeRateStored) (Exp 2.22e-3)
