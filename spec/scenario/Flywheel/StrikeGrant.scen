-- Tests for the grants and math patch

Macro DeployComptroller price=1.0
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1

Macro SetupMarkets
    NewSToken ZRX sZRX
    NewSToken BAT sBAT
    Support sZRX collateralFactor:0.5
    Support sBAT collateralFactor:0.5

Macro SetupStrikeToken strikeInitAmount=5000000e18
    Erc20 Deploy Standard STRK "STRK Token" 18
    Give (Address Comptroller) strikeInitAmount STRK
    Comptroller Send "setSTRKAddress(address)" (Address STRK)

-- NewComptroller, but with markets listed so that we can make them STRK markets in constructor
Macro FlywheelComptroller price=1.0 borrowRate=0.000005 strikeInitAmount=5000000e18
    DeployComptroller price
    SetupMarkets
    SetupStrikeToken strikeInitAmount

Macro InitUsage
    Prep Geoff 100e18 ZRX sZRX
    Mint Geoff 50e18 sZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT sBAT
    Mint Coburn 6e18 sBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn sBAT
    Borrow Coburn 1e18 sZRX

Macro InitUsageAndSpeeds
    InitUsage
    Comptroller SetStrikeSpeeds (sZRX sBAT) (1 1) (1 1)

Test "STRK can be granted in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller StrikeAccrued Geoff) 0
    Assert Equal (Erc20 STRK TokenBalance Geoff) 0
    FastForward 1000 Blocks
    Comptroller ClaimStrike Geoff
    Comptroller Send "_grantStrk(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller StrikeAccrued Geoff) 0
    Assert Equal (Erc20 STRK TokenBalance Geoff) 2000 -- 1000 (grant) + 1000 (STRK supply rewards)

Test "STRK can be granted"
    -- Can be granted once
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller StrikeAccrued Geoff) 0
    Assert Equal (Erc20 STRK TokenBalance Geoff) 0
    Comptroller Send "_grantStrk(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller StrikeAccrued Geoff) 0
    Assert Equal (Erc20 STRK TokenBalance Geoff) 1000
    -- Assert Log StrikeGranted (recipient (Address Geoff)) (amount "1000")
    -- Can be granted multiple times
    Comptroller Send "_grantStrk(address,uint256)" (Address Geoff) 2000
    Assert Equal (Comptroller StrikeAccrued Geoff) 0
    Assert Equal (Erc20 STRK TokenBalance Geoff) 3000

Test "STRK can be streamed to contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller StrikeAccrued Torrey) 0
    Assert Equal (Erc20 STRK TokenBalance Torrey) 0
    Comptroller Send "_setContributorStrikeSpeed(address,uint256)" (Address Torrey) 300
    -- Assert Log ContributorStrikeSpeedUpdated (recipient (Address Torrey)) (amount "300")
    FastForward 1000 Blocks
    -- Just ClaimStrike does not receive STRK
    Comptroller ClaimStrike Torrey
    Assert Equal (Comptroller StrikeAccrued Torrey) 0
    Assert Equal (Erc20 STRK TokenBalance Torrey) 0
    -- Calling updateContributorRewards and then ClaimStrike receives STRK
    Comptroller UpdateContributorRewards Torrey
    Assert Equal (Comptroller StrikeAccrued Torrey) 300000
    Comptroller ClaimStrike Torrey
    Assert Equal (Comptroller StrikeAccrued Torrey) 0
    Assert Equal (Erc20 STRK TokenBalance Torrey) 300000

Test "STRK can be streamed in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorStrikeSpeed(address,uint256)" (Address Geoff) 300
    FastForward 1000 Blocks
    -- Just ClaimStrike does not receive STRK
    Comptroller UpdateContributorRewards Geoff
    Assert Equal (Comptroller StrikeAccrued Geoff) 300000
    Comptroller ClaimStrike Geoff
    Assert Equal (Comptroller StrikeAccrued Geoff) 0
    Assert Equal (Erc20 STRK TokenBalance Geoff) 301000 -- 300000 (contributer grant) + 1000 (STRK supply rewards)

Test "STRK streaming can be changed for contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorStrikeSpeed(address,uint256)" (Address Torrey) 300
    FastForward 1000 Blocks
    Comptroller Send "_setContributorStrikeSpeed(address,uint256)" (Address Torrey) 600
    FastForward 1000 Blocks
    Comptroller UpdateContributorRewards Torrey
    Comptroller ClaimStrike Torrey
    Assert Equal (Comptroller StrikeAccrued Torrey) 0
    Assert Equal (Erc20 STRK TokenBalance Torrey) 900000
    Comptroller Send "_setContributorStrikeSpeed(address,uint256)" (Address Torrey) 0
    FastForward 2000 Blocks
    Assert Equal (Comptroller StrikeAccrued Torrey) 0
    Assert Equal (Erc20 STRK TokenBalance Torrey) 900000