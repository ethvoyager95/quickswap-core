
Test "Simple sToken Transfer"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 sZRX TokenBalance Torrey) Zero
    -- Just transfer
    Transfer Geoff Torrey 10e9 sZRX
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 sZRX TokenBalance Torrey) (Exactly 10e9)

Test "Simple sToken Transfer When Underlying Paused"
    NewComptroller
    ListedSToken WBTC sWBTC initialExchangeRate:0.1 tokenType:WBTC
    Prep Geoff Some WBTC sWBTC
    Mint Geoff 50e8 sWBTC
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 sWBTC TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 sWBTC TokenBalance Torrey) Zero
    -- Just transfer
    Erc20 WBTC Pause
    Transfer Geoff Torrey 10e9 sWBTC
    Assert Equal (Erc20 sWBTC TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 sWBTC TokenBalance Torrey) (Exactly 10e9)

Test "Simple sToken Transfer 1:1 Rate"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:1e0
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 50e18)
    Assert Equal (Erc20 sZRX TokenBalance Torrey) Zero
    -- Just transfer
    Transfer Geoff Torrey 10e18 sZRX
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 40e18)
    Assert Equal (Erc20 sZRX TokenBalance Torrey) (Exactly 10e18)

Test "Simple sToken Transfer Not Allowed by Comptroller"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:1e0
    Comptroller SetCollateralFactor sZRX 0.1
    EnterMarkets Geoff sZRX
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    -- Just to be sure, check initial balances
    Invariant Remains (Erc20 sZRX TokenBalance Geoff) (Exactly 50e18)
    Invariant Remains (Erc20 sZRX TokenBalance Torrey) Zero
    -- Just transfer
    AllowFailures
    Transfer Geoff Torrey 60e18 sZRX
    Assert Failure COMPTROLLER_REJECTION TRANSFER_COMPTROLLER_REJECTION INSUFFICIENT_LIQUIDITY

Test "Simple sToken Transfer From"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    -- Just to be sure, check initial balances
    Invariant Remains (Erc20 sZRX TokenBalance Torrey) Zero
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 sZRX TokenBalance Coburn) Zero
    -- Add approval
    From Geoff (Erc20 sZRX Approve Torrey 25e9)
    Assert Equal (Erc20 sZRX Allowance Geoff Torrey) (Exactly 25e9)
    -- Perform transfer from
    From Torrey (Erc20 sZRX TransferFrom Geoff Coburn 10e9)
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 sZRX TokenBalance Coburn) (Exactly 10e9)
    Assert Equal (Erc20 sZRX Allowance Geoff Torrey) (Exactly 15e9)

Test "sToken Transfer From Not Allowed"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    --
    Invariant Remains (Erc20 sZRX TokenBalance Geoff) (Exactly 50e9)
    Invariant Remains (Erc20 sZRX TokenBalance Torrey) (Exactly 0e9)
    AllowFailures
    Erc20 sZRX TransferFrom Geoff Torrey 10e9
    Assert Failure MATH_ERROR TRANSFER_NOT_ALLOWED

Test "sToken Transfer paused"
    NewComptroller
    ListedSToken ZRX sZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX sZRX
    Mint Geoff 50e18 sZRX
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 sZRX TokenBalance Coburn) Zero
    -- Pause and attempt transfer
    Comptroller SetPauseGuardian Coburn
    From Coburn (Comptroller SetGuardianPaused "Transfer" True)
    AllowFailures
    Transfer Geoff Torrey 10e9 sZRX
    Assert Revert "revert transfer is paused"
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 sZRX TokenBalance Coburn) Zero
    -- unPause and complete transfer
    Invariant Success
    Comptroller SetGuardianPaused "Transfer" False
    Transfer Geoff Coburn 10e9 sZRX
    Assert Equal (Erc20 sZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 sZRX TokenBalance Coburn) (Exactly 10e9)
